import itertools
import os
import collections
import json
from snakemake.utils import R
from snakemake.exceptions import MissingInputException
NGS_PIPELINE=os.environ['NGS_PIPELINE']
WORK_DIR=os.environ['WORK_DIR']
HOST=os.environ['HOST']
configfile: NGS_PIPELINE +"/config/config_annotation.json"
configfile: NGS_PIPELINE +"/config/config_common.json"
configfile: NGS_PIPELINE +"/config/config_cluster.json"
if HOST == 'biowulf.nih.gov':
        configfile: NGS_PIPELINE +"/config/config_common_biowulf.json"
elif HOST == 'login01':
        configfile: NGS_PIPELINE +"/config/config_common_tgen.json"

config['host'] = HOST
GROUP=config['group']
#HOST = config['host']
###########################################################################
#
#               This initializes all the variables we need for the jobs.
#               It also removes the host specific constraints like scratch
#               area on the node.
#               module purge is needed to remove all the loaded modules and
#                       inside the rule load what is necessary.
###########################################################################
shell.prefix("""
set -e -o pipefail
module purge
if [ {HOST} == 'biowulf.nih.gov' ]
        then
                MEM=`echo "${{SLURM_MEM_PER_NODE}} / 1024 "|bc`
                LOCAL="/lscratch/${{SLURM_JOBID}}/"
                THREADS=${{SLURM_CPUS_ON_NODE}}
elif [ {HOST} == 'login01' ]
        then
                module load torque/4.2.2
                module load gcc/4.8.1
                MEM=`qstat -f ${{PBS_JOBID}} |grep Resource_List.mem |perl -n -e'/mem = (\d+)gb/ && print \$1'`
                mkdir -p /projects/scratch/${{PBS_JOBID}}/
                LOCAL="/projects/scratch/${{PBS_JOBID}}/"
                THREADS=${{PBS_NUM_PPN}}
fi
""")

#PATIENTS =[]
#for subject in config['subject'].keys():
#        PATIENTS.append(subject)
ANNO_FILES =["gene","clinseq","cadd","cosmic","pcg","clinvar","hgmd","match","mcg","docm","candl","tcc","civic","sift.out","coding.rare.txt","annotations.final.txt"]
ALL_QC = expand("AnnotationInput.{suff}", suff=ANNO_FILES)
rule final:
        input:
                ALL_QC
############
#	table_annovar for:
#		refGene
#		cytoBand
#		snp138
#		1000g2014oct_all
#		1000g2014oct_eur
#		1000g2014oct_afr
#		1000g2014oct_amr
#		1000g2014oct_eas
#		1000g2014oct_sas
#		esp6500_all
#		esp6500_ea
#		esp6500_aa
#		exac03nontcga
#		exac03
#		cg69
#		nci60
############
rule Annovar_Gene:
	input:
		config["annovar_data"]+config["annot_refgene"],
		config["annovar_data"]+config["annot_mrna"],
		config["annovar_data"]+config["annot_cb"],
		config["annovar_data"]+config["annot_snp138"],
		config["annovar_data"]+config["annot_all"],
		config["annovar_data"]+config["annot_eur"],
		config["annovar_data"]+config["annot_afr"],
		config["annovar_data"]+config["annot_amr"],
		config["annovar_data"]+config["annot_eas"],
		config["annovar_data"]+config["annot_sas"],
		config["annovar_data"]+config["annot_espall"],
		config["annovar_data"]+config["annot_espea"],
		config["annovar_data"]+config["annot_espaa"],
		config["annovar_data"]+config["annot_exacnon"],
		config["annovar_data"]+config["annot_exac"],
		config["annovar_data"]+config["annot_cg69"],
		config["annovar_data"]+config["annot_nci60"],
		file="AnnotationInput.anno",
	output:
		"AnnotationInput.gene"
	version: config["annovar"]
	params:
		rulename   = "Annot_gene",
		batch      = config[config['host']]["job_annovar"],
		RefData    = config["annovar_data"],
		build      = config["build"],
	shell: """
	#######################
	module load annovar/{version}
	table_annovar.pl {input.file} {params.RefData} -buildver {params.build} -out {input.file}.gene -remove -protocol refGene,cytoBand,snp138,1000g2014oct_all,1000g2014oct_eur,1000g2014oct_afr,1000g2014oct_amr,1000g2014oct_eas,1000g2014oct_sas,esp6500_all,esp6500_ea,esp6500_aa,exac03nontcga,exac03,cg69,nci60 -operation g,r,f,f,f,f,f,f,f,f,f,f,f,f,f,f -nastring "-1" --argument "-hgvs",,,,,,,,,,,,,,,
	mv {input.file}.gene.{params.build}_multianno.txt {output}
	sed -i '1s/\./_/g' {output}
	rm -rf {input.file}.gene.refGene.invalid_input {input.file}.gene.invalid_input
	#######################
	"""
############
# annotate_variation
#	custom ref input. clinseq
############
rule Annovar_Clinseq:
	input:
		file="AnnotationInput.anno",
		clinseq=config["annovar_data"]+config["annot_clinseq"]
	output: "AnnotationInput.clinseq"
	version: config["annovar"]
	params:
		rulename   = "Annot_clinseq",
		batch      = config[config['host']]["job_annovar"],
		RefData    = config["annovar_data"],
		build      = config["build"],
	shell: """
	#######################
	module load annovar/{version}
	annotate_variation.pl {input.file} {params.RefData} -buildver {params.build} -otherinfo -filter -dbtype generic -genericdbfile `basename {input.clinseq}`
	awk '{{OFS="\\t"}};{{print $3,$4,$5,$6,$7,$2}}' {input.file}.{params.build}_generic_dropped |sed -e 's/,/\\t/g' >{output}
	head -1 {input.clinseq} >>{output}
	rm -rf {input.file}.{params.build}_generic_dropped {input.file}.{params.build}_generic_filtered {input.file}.invalid_input
	#######################
	"""
################################
# Add CADD annotation
#
################################
rule Annovar_CADD:
	input:
		file="AnnotationInput.anno",
		cadd=config["annovar_data"]+config["annot_cadd"],
		cadd_indel=config["annovar_data"]+config["annot_caddind"]
	output:
		"AnnotationInput.cadd",
	version: config["annovar"]
	params:
		rulename   = "Annot_CADD",
		batch      = config[config['host']]["job_annovar"],
		RefData    = config["annovar_data"],
		build      = config["build"],
	shell: """
	#######################
	module load annovar/{version}
	annotate_variation.pl {input.file} {params.RefData} -buildver {params.build} -otherinfo -filter -dbtype cadd
	annotate_variation.pl {input.file}.{params.build}_cadd_filtered {params.RefData} -buildver {params.build} -otherinfo -filter -dbtype caddindel
	cut -f 2-7 {input.file}.{params.build}_cadd_dropped {input.file}.{params.build}_cadd_filtered.{params.build}_caddindel_dropped |sed -e 's/,/\\t/g' |awk '{{OFS="\\t"}};{{print $3,$4,$5,$6,$7,$1,$2}}' >{output}
	head -1 {input.cadd_indel} >>{output}
	rm -rf {input.file}.{params.build}_cadd_dropped {input.file}.{params.build}_cadd_filtered {input.file}.{params.build}_cadd_filtered.log {input.file}.{params.build}_cadd_filtered.{params.build}_caddindel_filtered {input.file}.{params.build}_cadd_filtered.{params.build}_caddindel_dropped {input.file}.invalid_input
	
	#######################
	"""
################################
# Add COSMIC
#
################################
rule Annovar_COSMIC:
	input:
		file="AnnotationInput.anno",
		cosmic=config["annovar_data"]+config["annot_cosmic"]
	output:
		"AnnotationInput.cosmic",
	version: config["annovar"]
	params:
		rulename   = "Annot_COSMIC",
		batch      = config[config['host']]["job_annovar"],
		RefData    = config["annovar_data"],
		build      = config["build"],
	shell: """
	#######################
	module load annovar/{version}
	table_annovar.pl {input.file} {params.RefData} -buildver {params.build} --dot2underline -out {input.file}.cosmic -remove -protocol cosmic78 -operation f -nastring "NA" 
	mv {input.file}.cosmic.{params.build}_multianno.txt {output}
	rm -rf {input.file}.cosmic.invalid_input
	#######################
	"""
################################
# Add PCG
#
################################
rule Annovar_PCG:
	input:
		file="AnnotationInput.anno",
		pcg=config["annovar_data"]+config["annot_pcg"]
	output:
		"AnnotationInput.pcg",
	version: config["annovar"]
	params:
		rulename   = "Annot_PCG",
		batch      = config[config['host']]["job_annovar"],
		RefData    = config["annovar_data"],
		build      = config["build"],
	shell: """
	#######################
	module load annovar/{version}
	annotate_variation.pl {input.file} {params.RefData} -buildver {params.build} -otherinfo --outfile {input.file}.pcg -filter -dbtype generic -genericdbfile `basename {input.pcg}`
	awk -F "\\t" '{{OFS="\\t"}};{{print $3,$4,$5,$6,$7,$2}}' {input.file}.pcg.{params.build}_generic_dropped |sed -e 's/,/\\t/g' >{output}
	head -1 {input.pcg} >>{output}
	rm -rf {input.file}.pcg.{params.build}_generic_dropped {input.file}.pcg.{params.build}_generic_filtered {input.file}.invalid_input
	#######################
	"""
################################
# Add HGMD
#
################################
rule Annot_Custom:
	input:
		tool	=NGS_PIPELINE + "/scripts/addAnnotation.pl",
		file	="AnnotationInput.anno",
		clinvar	=config["annovar_data"]+config["annot_clinvar"],
		hgmd	=config["annovar_data"]+config["annot_hgmd"],
		match	=config["annovar_data"]+config["annot_match"],
		mcg	=config["annovar_data"]+config["annot_mcg"],
		docm	=config["annovar_data"]+config["annot_docm"],
		candl	=config["annovar_data"]+config["annot_candl"],
		tcc	=config["annovar_data"]+config["annot_tcc"],
		civic	=config["annovar_data"]+config["annot_civic"]
	output:
		clinvar="AnnotationInput.clinvar",
		hgmd   ="AnnotationInput.hgmd",
		match  ="AnnotationInput.match",
		mcg    ="AnnotationInput.mcg",
		docm   ="AnnotationInput.docm",
		candl  ="AnnotationInput.candl",
		tcc    ="AnnotationInput.tcc",
		civic  ="AnnotationInput.civic"	
	params:
		rulename   = "Annot_PCG",
		batch      = config[config['host']]["job_annovar"],
		RefData    = config["annovar_data"],
		build      = config["build"],
	shell: """
	#######################
	module load annovar
	{input.tool} {input.clinvar}	{input.file} >{output.clinvar}
	{input.tool} {input.hgmd}	{input.file} >{output.hgmd}
	{input.tool} {input.match}	{input.file} >{output.match}
	{input.tool} {input.mcg}	{input.file} >{output.mcg}
	{input.tool} {input.docm}	{input.file} >{output.docm}
	{input.tool} {input.candl}	{input.file} >{output.candl}
	{input.tool} {input.tcc}	{input.file} >{output.tcc}
	{input.tool} {input.civic}	{input.file} >{output.civic}	
	#######################
	"""
############
#       SIFT
############
rule SIFT:
	input:
		sift	="AnnotationInput.sift",
		tool	=NGS_PIPELINE + "/scripts/ParseSIFT.pl"
	output:
		"AnnotationInput.sift.out"
	version: config["SIFT"]
	resources: SIFT=1
	params:
		rulename   = "SIFT",
		batch      = config[config['host']]["job_SIFT"],
		build      = config["SIFTbuild"]
	shell: """
	#######################
	if [ -s {input.sift} ]; then
		module load python/2.7.9
		module load SIFT/{version}
		DIR=`pwd`
		cd ${{DIR}}/`dirname {input.sift}`
		FILE=`basename {input.sift}`
		SIFT_exome_nssnvs.pl -i ${{FILE}} -d $SIFTDB/Human_db_37 -o ${{LOCAL}}  -z ${{DIR}}/`dirname {input.sift}`/${{FILE}}.sift_predictions.tsv
		perl {input.tool} ${{DIR}}/`dirname {input.sift}`/${{FILE}}.sift_predictions.tsv >${{DIR}}/`dirname {input.sift}`/${{FILE}}.out
	else
		echo -e "Chr\\tStart\\tEnd\\tRef\\tAlt\\tSIFT Prediction\\tSIFT Score" >{output}
	fi
	rm -rf ${{DIR}}/{input.sift}.sift_predictions.tsv
	#######################
	"""
############
#	Combine Annotation
############
rule CombineAnnotation:
	input:
		"AnnotationInput.gene",
		"AnnotationInput.clinseq",
		"AnnotationInput.cadd",
		"AnnotationInput.cosmic",
		"AnnotationInput.pcg",
		"AnnotationInput.clinvar",
		"AnnotationInput.hgmd",
		"AnnotationInput.match",
		"AnnotationInput.mcg",
		"AnnotationInput.docm",
		"AnnotationInput.candl",
		"AnnotationInput.tcc",
		"AnnotationInput.civic",
		"AnnotationInput.sift.out",
		convertor   = NGS_PIPELINE + "/scripts/CombineAnnotations.pl",
		geneanno    = NGS_PIPELINE + "/scripts/GeneAnnotation.pl",
		filter      = NGS_PIPELINE + "/scripts/filterVariants.v1.pl",
		coding      = NGS_PIPELINE + "/scripts/ProteinCoding.pl",
		blacklisted = config["annovar_data"]+config["blacklisted"],
		ACMG	    = config["annovar_data"]+config['ACMG']
	output: 
		filtered="AnnotationInput.coding.rare.txt",
		all	="AnnotationInput.annotations.final.txt"
	version: "1.0"
	params:
		rulename   = "combine",
		batch	   = config[config['host']]["job_Combine"],
		dataDir    = config["annovar_data"]
	shell: """
	#######################
echo "AnnotationInput
AnnotationInput.gene
AnnotationInput.clinseq
AnnotationInput.cadd
AnnotationInput.sift.out
AnnotationInput.clinvar
AnnotationInput.cosmic
AnnotationInput.hgmd
AnnotationInput.match
AnnotationInput.docm
AnnotationInput.candl
AnnotationInput.tcc
AnnotationInput.mcg
AnnotationInput.civic
AnnotationInput.pcg" >list
	perl {input.convertor} list >{output.all}.tmp
	perl {input.geneanno} {input.ACMG} {output.all}.tmp >{output.all}

	perl {input.coding} AnnotationInput.annotations.final.txt | perl {input.filter} - {input.blacklisted} 0.05 >{output.all}.tmp
	grep -P "Chr\\tStart\\tEnd\\tRef\\tAlt" {output.all}.tmp >{output.filtered}
	grep -v -P "Chr\\tStart\\tEnd\\tRef\\tAlt" {output.all}.tmp >>{output.filtered}
	rm -rf {output.all}.tmp list
	#######################
	"""
